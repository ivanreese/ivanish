browser_sync = require("browser-sync").create()
del = require "del"
gulp = require "gulp"
gulp_autoprefixer = require "gulp-autoprefixer"
gulp_coffee = require "gulp-coffee"
gulp_concat = require "gulp-concat"
gulp_htmlmin = require "gulp-htmlmin"
gulp_kit = require "gulp-kit"
gulp_notify = require "gulp-notify"
gulp_rename = require "gulp-rename"
gulp_sass = require "gulp-sass"
gulp_uglify = require "gulp-uglify"


# CONFIG ##########################################################################################


assetTypes = "CNAME,gif,ico,jpeg,jpg,json,m4v,mp3,mp4,png,svg,swf,woff,xml"


paths =
  assets:
    source: "source/**/*{#{assetTypes}}"
  coffee:
    source: "source/script/**/*.coffee"
    watch: "source/script/**/*.coffee"
  kit:
    source: "source/pages/**/*.kit"
    watch: "source/**/*.kit" # pages + header-min.kit, header.kit
  pageCoffee:
    source: "source/pages/**/*.coffee"
    watch: "source/pages/**/*.coffee"
  scss:
    source: [
      "source/**/vars.scss"
      "source/**/*.scss"
    ]
    watch: "source/**/*.scss"


gulp_notify.logLevel(0)


# HELPER FUNCTIONS ################################################################################


logAndKillError = (err)->
  console.log "\n## Error ##"
  console.log err.toString() + "\n"
  gulp_notify.onError(
    emitError: true
    icon: false
    message: err.message
    title: "ðŸ‘»"
    wait: true
    )(err)
  @emit "end"


# TASKS: APP COMPILATION ##########################################################################


gulp.task "assets", ()->
  gulp.src paths.assets.source
    .pipe gulp_rename (path)->
      path.dirname = path.dirname.replace /.*\/pack\//, ''
      path
    .pipe gulp.dest "public"
    .pipe browser_sync.stream
      match: "**/*.{#{assetTypes}}"
        

gulp.task "coffee", ()->
  gulp.src paths.coffee.source
    .pipe gulp_concat "scripts.coffee"
    .pipe gulp_coffee()
    .pipe gulp_uglify()
    .on "error", logAndKillError
    .pipe gulp.dest "public"
    .pipe browser_sync.stream
      match: "**/*.js"


gulp.task "del:public", ()->
  del "public"

      
gulp.task "kit", ()->
  gulp.src paths.kit.source
    .pipe gulp_kit()
    .on "error", logAndKillError
    .pipe gulp_rename (path)->
      if path.basename is "index"
        path.dirname = ""
      else
        path.dirname = path.dirname + "/" + path.basename
        path.basename = "index"
      return path
    .pipe gulp_htmlmin
      collapseWhitespace: true
      collapseBooleanAttributes: true
      conservativeCollapse: false
      includeAutoGeneratedTags: false
      minifyCSS: true
      minifyJS: true
      removeComments: true
      sortAttributes: true
      sortClassName: true
    .pipe gulp.dest "public"
    .pipe browser_sync.stream
      match: "**/*.html"


gulp.task "pageCoffee", ()->
  gulp.src paths.pageCoffee.source
    .pipe gulp_coffee()
    .pipe gulp_uglify()
    .on "error", logAndKillError
    .pipe gulp_rename (path)->
      path.dirname = path.dirname + "/" + path.basename
    .pipe gulp.dest "public"
    .pipe browser_sync.stream
      match: "**/*.js"


gulp.task "scss", ()->
  gulp.src paths.scss.source
    .pipe gulp_concat "styles.scss"
    .pipe gulp_sass
      errLogToConsole: true
      outputStyle: "compressed"
      precision: 2
    .on "error", logAndKillError
    .pipe gulp_autoprefixer
      browsers: "last 5 Chrome versions, last 5 ff versions, IE >= 11, Safari >= 9, iOS >= 9"
      cascade: false
      remove: false
    .pipe gulp.dest "public"
    .pipe browser_sync.stream
      match: "**/*.css"
      

gulp.task "serve", ()->
  browser_sync.init
    ghostMode: false
    online: true
    server:
      baseDir: "public"
    ui: false


gulp.task "watch", (cb)->
  gulp.watch paths.assets.source, gulp.series "assets"
  gulp.watch paths.coffee.watch, gulp.series "coffee"
  gulp.watch paths.kit.watch, gulp.series "kit"
  gulp.watch paths.pageCoffee.watch, gulp.series "pageCoffee"
  gulp.watch paths.scss.watch, gulp.series "scss"
  cb()


gulp.task "default", gulp.series "del:public", gulp.parallel("assets", "coffee", "kit", "pageCoffee", "scss"), "watch", "serve"
